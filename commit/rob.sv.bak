module rob #(
    parameter ROB_SIZE = 16,
    parameter ROB_BITS = $clog2(ROB_SIZE)
)(
    input  logic clk,
    input  logic rst,

    // Allocate
    input  logic alloc_en,
    input  logic [4:0] rd_arch,
    input  logic [5:0] rd_phys,
    input  logic [5:0] old_phys,
    output logic [ROB_BITS-1:0] alloc_idx,

    // Writeback
    input  logic wb_en,
    input  logic [ROB_BITS-1:0] wb_idx,

    // Commit
    output logic commit_valid,
    output logic [4:0] commit_rd_arch,
    output logic [5:0] free_phys
);

    typedef struct packed {
        logic valid;
        logic done;
        logic [4:0] rd_arch;
        logic [5:0] rd_phys;
        logic [5:0] old_phys;
    } rob_entry_t;

    rob_entry_t rob_mem[ROB_SIZE];
    logic [ROB_BITS-1:0] head, tail;

    always_ff @(posedge clk or posedge rst) begin
        if (rst) begin
            head <= '0;
            tail <= '0;
            for (int i = 0; i < ROB_SIZE; i++) begin
                rob_mem[i].valid <= 1'b0;
                rob_mem[i].done  <= 1'b0;
            end
        end else begin

            // Allocate
            if (alloc_en) begin
                assert (rd_phys != 0)
                else $fatal("ROB ERROR: allocated phys reg 0");

                rob_mem[tail].valid    <= 1'b1;
                rob_mem[tail].done     <= 1'b0;
                rob_mem[tail].rd_arch  <= rd_arch;
                rob_mem[tail].rd_phys  <= rd_phys;
                rob_mem[tail].old_phys <= old_phys;
                tail <= tail + 1;
            end

            // Writeback
            if (wb_en)
                rob_mem[wb_idx].done <= 1'b1;

            // Commit
            if (rob_mem[head].valid && rob_mem[head].done) begin
                rob_mem[head].valid <= 1'b0;
                head <= head + 1;
            end
        end
    end

    assign alloc_idx       = tail;
    assign commit_valid    = rob_mem[head].valid && rob_mem[head].done;
    assign commit_rd_arch  = rob_mem[head].rd_arch;
    assign free_phys       = rob_mem[head].old_phys;

endmodule